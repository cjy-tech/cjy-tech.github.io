## 목차

* Terraform 과 IaC
* Terraform 설치
* 준비
* 인프라 구축
* 인프라 변경
* 인프라 제거
* 그래프

## 개요

훌륭한 IaC (Infrastructure as Code) 도구인 Terraform 을 사용하여 NHN Cloud 의 리소스(서버 인스턴스 등)를 만들고 변경하고 제거하는 예제를 정리해보았습니다.

본문에서는 Terraform 의 핵심적인 내용만 간단히 언급하기에 상세한 내용은 [Terraform 문서](https://www.terraform.io/docs/index.html) 를 참고하면 상세한 내용을 확인 할 수 있습니다.

예제의 관련 파일들은 [GitHub Gist](https://github.nhnent.com/gist/eunchul-jung/b8a1373c32f5504c4baaf1d0b4836a31) 에 있으니 참고 부탁드립니다.

NHN Cloud 의 Terraform 연동 관련해서는 [Terraform 사용 가이드](https://docs.toast.com/ko/Compute/Instance/ko/terraform-guide/) 에 자세히 설명되어 있기 때문에 참고 부탁드립니다.

## Terraform 과 IaC

Terraform 은 HashiCorp 라는 매후 훌륭한 회사에서 개발되어 제공되는 아주 훌륭한 IaC 도구입니다.
안전하고 반복 가능한 방식으로 인프라를 구축, 변경 및 관리할 수 있는 도구입니다.
이 외에도 이미지 도구인 Packer, 개발환경을 쉽게 구성할 수 있게 해주는 Vagrant, Secrets 관리도구인 Vault、Service mesh 도구 Consul 등등이 있습니다.

### 작업의 흐름(Workflows)

Terraform 을 사용한 IaC 행위는 코드를 작성하고 변경을 확인하고 적용하는 3단계가 반복되게 됩니다.
<br>
1. Write - 인프라를 코드로 작성
2. Plan - 반영하기(applying) 전에 변경을 확인
3. Apply - 변경 내용을 적용.

### State

처음 초기화(Initialize) 를 하면 state file 을 생성합니다.
Terraform 은 이 local state 를 사용하여 plans 을 생성하고 인프라를 변경합니다.
즉, 작업전 어떤 변경이 일어날지 판단하는 기준이 됩니다.
마치 CMDB(사내에선 Tips) 의 역할이라고 볼수 있을것 같습니다.

이 중요한 state file 은 local state 또는 remote state 로 저장 및 관리할 수 있습니다.
기본적으로는 local 이 사용되지만 Team 단위로 Terraform 을 사용한다면 remote state 를 사용하는 편이 유익할 수 있습니다.

## Terraform 설치

[Download](https://www.terraform.io/downloads.html) 페이지에서 OS, 아키텍쳐에 맞는것을 다운로드 받고 압축을 해제하면 바로 사용할 수 있습니다.

Terraform 은 Go 언어로 작성되었고 배포되는 바이너리는 정적 컴파일(statically-compiled)된 바이너리입니다. 그래서 별도의 라이브러리 의존성이 없지만 용량은 좀 큰 편입니다.

제 로컬 환경은 Debian Linux 라 본문의 예제는 Linux 64-bit 의 것을 사용했습니다.
<br>
```
$ terraform version
Terraform v0.14.7

Your version of Terraform is out of date! The latest version
is 0.14.8. You can update by downloading from https://www.terraform.io/downloads.html
```

업데이트가 있다고 알려주어 업데이트를 했습니다.
<br>
```
$ terraform version
Terraform v0.14.8
```

## 준비

NHN Cloud 는 OpenStack 의 API 를 사용해야 하기 때문에 [terraform-provider-openstack/openstack](https://registry.terraform.io/providers/terraform-provider-openstack/openstack/latest/docs) 라는 provider 를 사용해야 합니다. (버전 0.13 부터 변경되었습니다.)

먼저 작업 디렉토리에서 `updong` 이라는 디렉토리를 생성후 해당 디렉토리로 변경합니다.
<br>
```
$ mkdir updong
$ cd updong
```

### terraform 정의

`config.tf` 파일을 만들고 다음과 같이 내용을 추가합니다.
<br>
```
terraform {
  required_version = ">= 0.14.0"
  required_providers {
    openstack = {
      source  = "terraform-provider-openstack/openstack"
      version = "~> 1.35.0"
    }
  }
  backend "swift" {}
}
```

`backend` 는 remote state 저장소로 사용하기 위한 설정입니다.
`terraform init` 을 수행할때 별도로 속성값을 전달하기 위해 빈 블럭으로 작성했습니다. 이유는 코드에 고정된 값을 최대한 사용하지 않으며 민감한 정보를 가능한 숨기기 위해서 입니다.

`backend.hcl` 파일에 backend 관련 속성을 기술합니다.
<br>
```
region_name       = "JP1"
auth_url          = "https://api-identity.infrastructure.cloud.toast.com/v2.0"
tenant_id         = "<swift_tenant_id>"
user_name         = "<user_email>"
password          = "<secret>"
container         = "tfstate.d"
```

### tfvars 선언과 사용

Terraform 은 외부로 부터 변수값을 입력받아 리소스 구성을 동적으로 할 수 있습니다.
provider 속성과 서버 인스턴스를 tfvars 파일로 선언을 하여 tf 파일들은 가능한 변경이 발생안되도록 합니다.

`k3s-cluster.tfvars` 라는 파일에 다음의 내용을 추가합니다.
<br>
```
provider_openstack = {
  auth_url  = "https://api-identity.infrastructure.cloud.toast.com/v2.0"
  tenant_id = "<compute_tenant_id>"
  password  = "<secret>"
  user_name = "<user_email>"
  region    = "JP1"
}

machines = [
  {
    name = "k3s-node-01"
    flavor_name = "t2.c1m1"
    key_pair = "<key_pair_name>"
  },
]
```

선언된 tfvars 의 내용을 사용하기 위해서 변수 선언이 필요합니다.

`var.tf` 파일에 다음의 내용을 추가합니다.
<br>
```
variable "provider_openstack" {
  type = map
}

variable "machines" {
  type = list
}
```

map 타입의 provider\_openstack 이란 변수와 list 타입의 machines 변수를 정의했습니다.
plan 이나 apply 를 할때 `-var-file` 옵션에 tfvars 파일을 명시해주면 각 변수명으로 값이 정의되어 Terraform 에서 사용할 수 있게 됩니다.

### provider 정의

`provider.tf` 파일에 다음의 내용을 추가합니다.
<br>
```
provider "openstack" {
  auth_url  = var.provider_openstack.auth_url
  tenant_id = var.provider_openstack.tenant_id
  password  = var.provider_openstack.password
  user_name = var.provider_openstack.user_name
  region    = var.provider_openstack.region
}
```

### 초기화

여기까지 했으면 다음과 같이 초기화를 실행할수 있습니다.
<br>
```
$ ls
backend.hcl  config.tf  k3s-cluster.tfvars  provider.tf  var.tf

$ terraform init -backend-config=backend.hcl

Initializing the backend...

Successfully configured the backend "swift"! Terraform will automatically
use this backend unless the backend configuration changes.

Initializing provider plugins...
- terraform.io/builtin/terraform is built in to Terraform
- Finding terraform-provider-openstack/openstack versions matching "~> 1.35.0"...
- Installing terraform-provider-openstack/openstack v1.35.0...
- Installed terraform-provider-openstack/openstack v1.35.0 (self-signed, key ID 4F80527A391BEFD2)

Partner and community providers are signed by their developers.
If you'd like to know more about provider signing, you can read about it here:
https://www.terraform.io/docs/cli/plugins/signing.html

Terraform has created a lock file .terraform.lock.hcl to record the provider
selections it made above. Include this file in your version control repository
so that Terraform can guarantee to make the same selections by default when
you run "terraform init" in the future.

Terraform has been successfully initialized!

You may now begin working with Terraform. Try running "terraform plan" to see
any changes that are required for your infrastructure. All Terraform commands
should now work.

If you ever set or change modules or backend configuration for Terraform,
rerun this command to reinitialize your working directory. If you forget, other
commands will detect it and remind you to do so if necessary.
```

init 이 완료되면 Object Storage 에 `tfstate.d`, `tfstate.d.archive` 컨테이너가 생성된것을 확인할 수 있습니다.

`terraform providers` 명령으로 provider 의 구성정보를 확인해 볼수 있습니다.
<br>
```
$ terraform providers

Providers required by configuration:
.
└── provider[registry.terraform.io/terraform-provider-openstack/openstack] ~> 1.35.0
```

### workspace

Terraform 에서 workspace 를 분리하여 인프라를 관리 할 수 있습니다.
기본은 `default` workspace 이고, `k3s-cluster` 라는 workspace 를 만들어 보겠습니다.
remote state file 은 `tfstate.d/env-${terraform.workspace}/tfstate.tf` 로 저장이 되기 때문에 workspace 를 활용하면 작성한 tf 코드를 쉽게 재사용 할 수 있습니다.
<br>
```
$ terraform workspace list
* default

$ terraform workspace new k3s-cluster
Created and switched to workspace "k3s-cluster"!

You're now on a new, empty workspace. Workspaces isolate their state,
so if you run "terraform plan" Terraform will not see any existing state
for this configuration.
```

이제 Object Storage 를 확인해보면 `env-k3s-cluster` 컨테이너에 `tfstate.tf` 오브젝트가 생성된 것을 확인할 수 있습니다.

## 인프라 구축

이제 실제 서버 인스턴스를 생성해보겠습니다.
서버 인스턴스를 만들기 위해서 필요한 요소들이 있습니다.
그 요소들의 정보를 provider 를 통해 얻어오는 것이 `data` 입니다.
그리고 실제 리소스를 만드는 것이 `resource` 입니다.

먼저 vpc, subnet, security group, image 정보를 얻어오는 `data.tf` 파일을 생성하여 다음의 내용을 추가합니다.
<br>
```
data "openstack_networking_network_v2" "vpc" {
  name = "Default Network"
}

data "openstack_networking_subnet_v2" "subnet" {
  network_id = data.openstack_networking_network_v2.vpc.id
  name = "Default Network"
}

data "openstack_networking_secgroup_v2" "sg" {
  name = "default"
}

data "openstack_images_image_v2" "img_debian10" {
  name = "Debian 10.7 Buster (2020.12.22)"
  most_recent = true
}
```

그리고 `resource.tf` 파일에 다음의 내용을 추가합니다.
<br>
```
resource "openstack_networking_port_v2" "nic" {
  count = length(var.machines)

  network_id = data.openstack_networking_network_v2.vpc.id
  fixed_ip {
    subnet_id = data.openstack_networking_subnet_v2.subnet.id
  }
  security_group_ids = [
    data.openstack_networking_secgroup_v2.sg.id
  ]
}

resource "openstack_compute_instance_v2" "vm" {
  count = length(var.machines)

  name 			= var.machines[count.index].name
  flavor_name 		= var.machines[count.index].flavor_name
  key_pair 		= var.machines[count.index].key_pair

  block_device {
    uuid = data.openstack_images_image_v2.img_debian10.id
    source_type = "image"
    destination_type = "volume"
    delete_on_termination = true
    volume_size = 20
    volume_type = "General HDD"
  }

  network {
    port = openstack_networking_port_v2.nic[count.index].id
  }
}
```

작업 디렉토리는 다음과 같습니다.
<br>
```
$ ls
backend.hcl  config.tf  data.tf  k3s-cluster.tfvars  provider.tf  resource.tf  var.tf
```

plan 을 실행해 보겠습니다.
<br>
```
$ terraform plan -var-file=k3s-cluster.tfvars

An execution plan has been generated and is shown below.
Resource actions are indicated with the following symbols:
  + create

Terraform will perform the following actions:

  # openstack_compute_instance_v2.vm[0] will be created
  + resource "openstack_compute_instance_v2" "vm" {
      + access_ip_v4        = (known after apply)
      + access_ip_v6        = (known after apply)
      + all_metadata        = (known after apply)
      + all_tags            = (known after apply)
      + availability_zone   = (known after apply)
      + flavor_id           = (known after apply)
      + flavor_name         = "t2.c1m1"
      + force_delete        = false
      + id                  = (known after apply)
      + image_id            = (known after apply)
      + image_name          = (known after apply)
      + key_pair            = "<key_name>"
      + name                = "k3s-node-01"
      + power_state         = "active"
      + region              = (known after apply)
      + security_groups     = (known after apply)
      + stop_before_destroy = false

      + block_device {
          + delete_on_termination = true
          + destination_type      = "volume"
          + source_type           = "image"
          + uuid                  = "dee7d040-76a2-4ec0-b79c-ad6978ec08a4"
          + volume_size           = 20
          + volume_type           = "General HDD"
        }

      + network {
          + access_network = false
          + fixed_ip_v4    = (known after apply)
          + fixed_ip_v6    = (known after apply)
          + floating_ip    = (known after apply)
          + mac            = (known after apply)
          + name           = (known after apply)
          + port           = (known after apply)
          + uuid           = (known after apply)
        }
    }

  # openstack_networking_port_v2.nic[0] will be created
  + resource "openstack_networking_port_v2" "nic" {
      + admin_state_up         = (known after apply)
      + all_fixed_ips          = (known after apply)
      + all_security_group_ids = (known after apply)
      + all_tags               = (known after apply)
      + device_id              = (known after apply)
      + device_owner           = (known after apply)
      + dns_assignment         = (known after apply)
      + dns_name               = (known after apply)
      + id                     = (known after apply)
      + mac_address            = (known after apply)
      + network_id             = "3ed0220a-6c92-41a3-ad40-1b7c57127622"
      + port_security_enabled  = (known after apply)
      + qos_policy_id          = (known after apply)
      + region                 = (known after apply)
      + security_group_ids     = [
          + "32436712-1f5e-4b38-93af-480f08513ba9",
        ]
      + tenant_id              = (known after apply)

      + binding {
          + host_id     = (known after apply)
          + profile     = (known after apply)
          + vif_details = (known after apply)
          + vif_type    = (known after apply)
          + vnic_type   = (known after apply)
        }

      + fixed_ip {
          + subnet_id = "152443f7-d96c-4cf9-9ddf-2cf7fe4b8b17"
        }
    }

Plan: 2 to add, 0 to change, 0 to destroy.

------------------------------------------------------------------------

Note: You didn't specify an "-out" parameter to save this plan, so Terraform
can't guarantee that exactly these actions will be performed if
"terraform apply" is subsequently run.
```

2 개를 add 할 계획이라고 알려줍니다.
바로 apply 를 해보겠습니다.
<br>
```
terraform apply -var-file=k3s-cluster.tfvars

An execution plan has been generated and is shown below.
Resource actions are indicated with the following symbols:
  + create

Terraform will perform the following actions:

  # openstack_compute_instance_v2.vm[0] will be created
  + resource "openstack_compute_instance_v2" "vm" {
      + access_ip_v4        = (known after apply)
      + access_ip_v6        = (known after apply)
      + all_metadata        = (known after apply)
      + all_tags            = (known after apply)
      + availability_zone   = (known after apply)
      + flavor_id           = (known after apply)
      + flavor_name         = "t2.c1m1"
      + force_delete        = false
      + id                  = (known after apply)
      + image_id            = (known after apply)
      + image_name          = (known after apply)
      + key_pair            = "<key_name>"
      + name                = "k3s-node-01"
      + power_state         = "active"
      + region              = (known after apply)
      + security_groups     = (known after apply)
      + stop_before_destroy = false

      + block_device {
          + delete_on_termination = true
          + destination_type      = "volume"
          + source_type           = "image"
          + uuid                  = "dee7d040-76a2-4ec0-b79c-ad6978ec08a4"
          + volume_size           = 20
          + volume_type           = "General HDD"
        }

      + network {
          + access_network = false
          + fixed_ip_v4    = (known after apply)
          + fixed_ip_v6    = (known after apply)
          + floating_ip    = (known after apply)
          + mac            = (known after apply)
          + name           = (known after apply)
          + port           = (known after apply)
          + uuid           = (known after apply)
        }
    }

  # openstack_networking_port_v2.nic[0] will be created
  + resource "openstack_networking_port_v2" "nic" {
      + admin_state_up         = (known after apply)
      + all_fixed_ips          = (known after apply)
      + all_security_group_ids = (known after apply)
      + all_tags               = (known after apply)
      + device_id              = (known after apply)
      + device_owner           = (known after apply)
      + dns_assignment         = (known after apply)
      + dns_name               = (known after apply)
      + id                     = (known after apply)
      + mac_address            = (known after apply)
      + network_id             = "3ed0220a-6c92-41a3-ad40-1b7c57127622"
      + port_security_enabled  = (known after apply)
      + qos_policy_id          = (known after apply)
      + region                 = (known after apply)
      + security_group_ids     = [
          + "32436712-1f5e-4b38-93af-480f08513ba9",
        ]
      + tenant_id              = (known after apply)

      + binding {
          + host_id     = (known after apply)
          + profile     = (known after apply)
          + vif_details = (known after apply)
          + vif_type    = (known after apply)
          + vnic_type   = (known after apply)
        }

      + fixed_ip {
          + subnet_id = "152443f7-d96c-4cf9-9ddf-2cf7fe4b8b17"
        }
    }

Plan: 2 to add, 0 to change, 0 to destroy.

Do you want to perform these actions in workspace "k3s-cluster"?
  Terraform will perform the actions described above.
  Only 'yes' will be accepted to approve.

  Enter a value: yes

openstack_networking_port_v2.nic[0]: Creating...
openstack_networking_port_v2.nic[0]: Creation complete after 8s [id=646d9b62-b87c-44ce-9940-72ec0778e9f0]
openstack_compute_instance_v2.vm[0]: Creating...
openstack_compute_instance_v2.vm[0]: Still creating... [10s elapsed]
openstack_compute_instance_v2.vm[0]: Still creating... [20s elapsed]
openstack_compute_instance_v2.vm[0]: Still creating... [30s elapsed]
openstack_compute_instance_v2.vm[0]: Still creating... [40s elapsed]
openstack_compute_instance_v2.vm[0]: Still creating... [50s elapsed]
openstack_compute_instance_v2.vm[0]: Creation complete after 51s [id=bbcdf286-787e-44e7-8ca9-052b28d390d6]

Apply complete! Resources: 2 added, 0 changed, 0 destroyed.
Releasing state lock. This may take a few moments...
```

apply 할때 기본적으로 실행여부를 물어보도록 되어있습니다.
물론 `-auto-approve` 옵션을 사용하면 건너뛸수 있습니다.

생성된 리소스중 하나는 `k3s-node-01` 이란 이름의 서버 인스턴스이고 다른 하나는 그 서버 인스턴스가 사용하는 network interface(port) 입니다.
그리고 이 리소스 정보는 remote state 에 저장됩니다.

`terraform show` 명령을 통해서 리소스 정보를 확인 할 수 있습니다.
<br>
```
$ terraform show
...(생략)
# openstack_compute_instance_v2.vm[0]:
resource "openstack_compute_instance_v2" "vm" {
    access_ip_v4        = "192.168.0.99"
    all_metadata        = {
        "description"      = "Debian 10.7 Buster (2020.12.22)"
        "hypervisor_type"  = "qemu"
        "image_name"       = "Debian 10.7 Buster (2020.12.22)"
        "login_username"   = "debian"
...(생략)
```

`terraform state list` 명령은 리소스 주소 목록을 확인할 수 있습니다.
<br>
```
$ terraform state list
data.openstack_images_image_v2.img_debian10
data.openstack_networking_network_v2.vpc
data.openstack_networking_secgroup_v2.sg
data.openstack_networking_subnet_v2.subnet
openstack_compute_instance_v2.vm[0]
openstack_networking_port_v2.nic[0]
```

그리고 `terraform state show <리소스 주소>` 명령으로 개별 리소스를 확인할 수 있습니다.
<br>
```
$ terraform state show 'openstack_compute_instance_v2.vm[0]'

# openstack_compute_instance_v2.vm[0]:
resource "openstack_compute_instance_v2" "vm" {
    access_ip_v4        = "192.168.0.99"
    all_metadata        = {
        "description"      = "Debian 10.7 Buster (2020.12.22)"
        "hypervisor_type"  = "qemu"
        "image_name"       = "Debian 10.7 Buster (2020.12.22)"
...(생략)
```

## 인프라 변경

현재 k3s-node-01 이라는 서버 인스턴스가 1대 있는 상황에서 `k3s-node-02` 라는 이름의 서버 인스턴스 1대를 추가해 보겠습니다.

`k3s-cluster.tfvars` 파일의 `machines` 에 항목을 추가합니다.
<br>
```
machines = [
  {
    name = "k3s-node-01"
    flavor_name = "t2.c1m1"
    key_pair = "<key_name>"
  },
+ {
+   name = "k3s-node-02"
+   flavor_name = "t2.c1m1"
+   key_pair = "<key_name>"
+ }
]
```

다시 plan 을 실행합니다.
<br>
```
$ terraform plan -var-file=k3s-cluster.tfvars
openstack_networking_port_v2.nic[0]: Refreshing state... [id=646d9b62-b87c-44ce-9940-72ec0778e9f0]
openstack_compute_instance_v2.vm[0]: Refreshing state... [id=bbcdf286-787e-44e7-8ca9-052b28d390d6]

An execution plan has been generated and is shown below.
Resource actions are indicated with the following symbols:
  + create

Terraform will perform the following actions:

  # openstack_compute_instance_v2.vm[1] will be created
  + resource "openstack_compute_instance_v2" "vm" {
      + access_ip_v4        = (known after apply)
      + access_ip_v6        = (known after apply)
      + all_metadata        = (known after apply)
      + all_tags            = (known after apply)
      + availability_zone   = (known after apply)
      + flavor_id           = (known after apply)
      + flavor_name         = "t2.c1m1"
      + force_delete        = false
      + id                  = (known after apply)
      + image_id            = (known after apply)
      + image_name          = (known after apply)
      + key_pair            = "<key_name>"
      + name                = "k3s-node-02"
      + power_state         = "active"
      + region              = (known after apply)
      + security_groups     = (known after apply)
      + stop_before_destroy = false

      + block_device {
          + delete_on_termination = true
          + destination_type      = "volume"
          + source_type           = "image"
          + uuid                  = "dee7d040-76a2-4ec0-b79c-ad6978ec08a4"
          + volume_size           = 20
          + volume_type           = "General HDD"
        }

      + network {
          + access_network = false
          + fixed_ip_v4    = (known after apply)
          + fixed_ip_v6    = (known after apply)
          + floating_ip    = (known after apply)
          + mac            = (known after apply)
          + name           = (known after apply)
          + port           = (known after apply)
          + uuid           = (known after apply)
        }
    }

  # openstack_networking_port_v2.nic[1] will be created
  + resource "openstack_networking_port_v2" "nic" {
      + admin_state_up         = (known after apply)
      + all_fixed_ips          = (known after apply)
      + all_security_group_ids = (known after apply)
      + all_tags               = (known after apply)
      + device_id              = (known after apply)
      + device_owner           = (known after apply)
      + dns_assignment         = (known after apply)
      + dns_name               = (known after apply)
      + id                     = (known after apply)
      + mac_address            = (known after apply)
      + network_id             = "3ed0220a-6c92-41a3-ad40-1b7c57127622"
      + port_security_enabled  = (known after apply)
      + qos_policy_id          = (known after apply)
      + region                 = (known after apply)
      + security_group_ids     = [
          + "32436712-1f5e-4b38-93af-480f08513ba9",
        ]
      + tenant_id              = (known after apply)

      + binding {
          + host_id     = (known after apply)
          + profile     = (known after apply)
          + vif_details = (known after apply)
          + vif_type    = (known after apply)
          + vnic_type   = (known after apply)
        }

      + fixed_ip {
          + subnet_id = "152443f7-d96c-4cf9-9ddf-2cf7fe4b8b17"
        }
    }

Plan: 2 to add, 0 to change, 0 to destroy.

------------------------------------------------------------------------

Note: You didn't specify an "-out" parameter to save this plan, so Terraform
can't guarantee that exactly these actions will be performed if
"terraform apply" is subsequently run.
```

plan 이나 apply 를 수행하기 전에 state file 의 내용을 비교하여 변경 정보를 알려줍니다.
이 경우는 2개의 리소스가 추가된다고 알려주고 있습니다.

다음 명령으로 apply 를 실행합니다.
`terraform apply -var-file=k3s-cluster.tfvars`

state list 정보를 확인해 보면 2개의 리소스가 늘어난 것을 확인할 수 있습니다.
<br>
```
$ terraform state list
data.openstack_images_image_v2.img_debian10
data.openstack_networking_network_v2.vpc
data.openstack_networking_secgroup_v2.sg
data.openstack_networking_subnet_v2.subnet
openstack_compute_instance_v2.vm[0]
openstack_compute_instance_v2.vm[1]
openstack_networking_port_v2.nic[0]
openstack_networking_port_v2.nic[1]
```

### taint

간혹 어떤 이유에 의해서 리소스를 다시 만들고 싶을때가 있습니다. 이경우 taint 를 사용하면 좋을수 있기 때문에 간단히 동작 확인을 해보겠습니다.

`openstack_compute_instance_v2.vm[1]` 의 id 는 `282637fe-c90a-4a3d-a7fe-e21e9f5c33c1` 입니다.
이 리소스에 taint 적용후 어떻게 변경이 되는지 확인해 보겠습니다.
<br>
```
$ terraform taint 'openstack_compute_instance_v2.vm[1]'

Resource instance openstack_compute_instance_v2.vm[1] has been marked as tainted.
```

plan 을 해보면 replace 한다고 알려줍니다.
<br>
```
$ terraform plan -var-file k3s-cluster.tfvars

openstack_networking_port_v2.nic[1]: Refreshing state... [id=3fa67069-7588-483e-afda-c8fa6fe56dc1]
openstack_networking_port_v2.nic[0]: Refreshing state... [id=646d9b62-b87c-44ce-9940-72ec0778e9f0]
openstack_compute_instance_v2.vm[0]: Refreshing state... [id=bbcdf286-787e-44e7-8ca9-052b28d390d6]
openstack_compute_instance_v2.vm[1]: Refreshing state... [id=282637fe-c90a-4a3d-a7fe-e21e9f5c33c1]

An execution plan has been generated and is shown below.
Resource actions are indicated with the following symbols:
-/+ destroy and then create replacement

Terraform will perform the following actions:

  # openstack_compute_instance_v2.vm[1] is tainted, so must be replaced
-/+ resource "openstack_compute_instance_v2" "vm" {
      ~ access_ip_v4        = "192.168.0.100" -> (known after apply)
      + access_ip_v6        = (known after apply)
      ~ all_metadata        = {
          - "description"      = "Debian 10.7 Buster (2020.12.22)"
          - "hypervisor_type"  = "qemu"
          - "image_name"       = "Debian 10.7 Buster (2020.12.22)"
          - "login_username"   = "debian"
          - "monitoring_agent" = "sysmon"
          - "os_architecture"  = "amd64"
          - "os_distro"        = "Debian"
          - "os_type"          = "linux"
          - "os_version"       = "10 Buster"
          - "project_domain"   = "NORMAL"
          - "tc_env"           = "sysmon,update_sudo"
          - "volume_size"      = "20"
        } -> (known after apply)
      ~ all_tags            = [] -> (known after apply)
      ~ availability_zone   = "jp-pub-b" -> (known after apply)
      ~ flavor_id           = "2718e9c1-b887-460b-bf4e-abcc2b010ec6" -> (known after apply)
      ~ id                  = "282637fe-c90a-4a3d-a7fe-e21e9f5c33c1" -> (known after apply)
      ~ image_id            = "Attempt to boot from volume - no image supplied" -> (known after apply)
      + image_name          = (known after apply)
        name                = "k3s-node-02"
      ~ region              = "JP1" -> (known after apply)
      ~ security_groups     = [
          - "default",
        ] -> (known after apply)
        # (5 unchanged attributes hidden)

      ~ block_device {
          - boot_index            = 0 -> null
            # (6 unchanged attributes hidden)
        }

      ~ network {
          ~ fixed_ip_v4    = "192.168.0.100" -> (known after apply)
          + fixed_ip_v6    = (known after apply)
          + floating_ip    = (known after apply)
          ~ mac            = "fa:16:3e:c2:91:34" -> (known after apply)
          ~ name           = "Default Network" -> (known after apply)
          ~ uuid           = "3ed0220a-6c92-41a3-ad40-1b7c57127622" -> (known after apply)
            # (2 unchanged attributes hidden)
        }
    }

Plan: 1 to add, 0 to change, 1 to destroy.

------------------------------------------------------------------------

Note: You didn't specify an "-out" parameter to save this plan, so Terraform
can't guarantee that exactly these actions will be performed if
"terraform apply" is subsequently run.
```

1개가 제거되고, 1개가 추가되는 방법으로 대체가 될것이라 알려줍니다.

다음 명령으로 apply 를 실행해봅니다.
<br>
```
$ terraform apply -var-file k3s-cluster.tfvars
...(생략)

openstack_compute_instance_v2.vm[1]: Destroying... [id=282637fe-c90a-4a3d-a7fe-e21e9f5c33c1]
openstack_compute_instance_v2.vm[1]: Still destroying... [id=282637fe-c90a-4a3d-a7fe-e21e9f5c33c1, 10s elapsed]
openstack_compute_instance_v2.vm[1]: Destruction complete after 11s
openstack_compute_instance_v2.vm[1]: Creating...
openstack_compute_instance_v2.vm[1]: Still creating... [10s elapsed]
openstack_compute_instance_v2.vm[1]: Still creating... [20s elapsed]
openstack_compute_instance_v2.vm[1]: Still creating... [30s elapsed]
openstack_compute_instance_v2.vm[1]: Still creating... [40s elapsed]
openstack_compute_instance_v2.vm[1]: Still creating... [50s elapsed]
openstack_compute_instance_v2.vm[1]: Still creating... [1m0s elapsed]
openstack_compute_instance_v2.vm[1]: Still creating... [1m10s elapsed]
openstack_compute_instance_v2.vm[1]: Still creating... [1m20s elapsed]
openstack_compute_instance_v2.vm[1]: Still creating... [1m30s elapsed]
openstack_compute_instance_v2.vm[1]: Creation complete after 1m36s [id=e4819f02-a442-4a0d-bf9e-d3fdfbc50533]

Apply complete! Resources: 1 added, 0 changed, 1 destroyed.
Releasing state lock. This may take a few moments...
```

기존 인스턴스가 삭제 되고 새로운 인스턴스가 기존의 Fixed IP 는 유지한채로 `k3s-node-02` 이름으로 만들어졌습니다.

이처럼 모든 인프라의 변경이 state 로 관리 되기 때문에 서비스 단위 또는 역할 단위의 인프라를 쉽게 관리 할 수 있는 점이 Terraform 의 특징입니다.

## 인프라 제거

제거하는 방법은 tfvars 의 내용을 변경하여 리소스를 제거하는 방법, destroy 명령으로 제거하는 방법 그리고 state rm 명령으로 리소스 단위를 명시적으로 제거하는 방법이 있습니다.

### terraform destroy

state 를 통해 관리되는 모든 리소스를 제거합니다.
<br>
```
$ terraform destroy -var-file k3s-cluster.tfvars

An execution plan has been generated and is shown below.
Resource actions are indicated with the following symbols:
  - destroy

Terraform will perform the following actions:

  # openstack_compute_instance_v2.vm[0] will be destroyed
  - resource "openstack_compute_instance_v2" "vm" {
      - access_ip_v4        = "192.168.0.99" -> null
      - all_metadata        = {
          - "description"      = "Debian 10.7 Buster (2020.12.22)"
          - "hypervisor_type"  = "qemu"
          - "image_name"       = "Debian 10.7 Buster (2020.12.22)"
          - "login_username"   = "debian"
          - "monitoring_agent" = "sysmon"
          - "os_architecture"  = "amd64"
          - "os_distro"        = "Debian"
          - "os_type"          = "linux"
          - "os_version"       = "10 Buster"
          - "project_domain"   = "NORMAL"
          - "tc_env"           = "sysmon,update_sudo"
          - "volume_size"      = "20"
        } -> null
      - all_tags            = [] -> null
      - availability_zone   = "jp-pub-b" -> null
      - flavor_id           = "2718e9c1-b887-460b-bf4e-abcc2b010ec6" -> null
      - flavor_name         = "t2.c1m1" -> null
      - force_delete        = false -> null
      - id                  = "bbcdf286-787e-44e7-8ca9-052b28d390d6" -> null
      - image_id            = "Attempt to boot from volume - no image supplied" -> null
      - key_pair            = "<key_name>" -> null
      - name                = "k3s-node-01" -> null
      - power_state         = "active" -> null
      - region              = "JP1" -> null
      - security_groups     = [
          - "default",
        ] -> null
      - stop_before_destroy = false -> null

      - block_device {
          - boot_index            = 0 -> null
          - delete_on_termination = true -> null
          - destination_type      = "volume" -> null
          - source_type           = "image" -> null
          - uuid                  = "dee7d040-76a2-4ec0-b79c-ad6978ec08a4" -> null
          - volume_size           = 20 -> null
          - volume_type           = "General HDD" -> null
        }

      - network {
          - access_network = false -> null
          - fixed_ip_v4    = "192.168.0.99" -> null
          - mac            = "fa:16:3e:b4:d8:4e" -> null
          - name           = "Default Network" -> null
          - port           = "646d9b62-b87c-44ce-9940-72ec0778e9f0" -> null
          - uuid           = "3ed0220a-6c92-41a3-ad40-1b7c57127622" -> null
        }
    }

  # openstack_compute_instance_v2.vm[1] will be destroyed
  - resource "openstack_compute_instance_v2" "vm" {
      - access_ip_v4        = "192.168.0.100" -> null
      - all_metadata        = {
          - "description"      = "Debian 10.7 Buster (2020.12.22)"
          - "hypervisor_type"  = "qemu"
          - "image_name"       = "Debian 10.7 Buster (2020.12.22)"
          - "login_username"   = "debian"
          - "monitoring_agent" = "sysmon"
          - "os_architecture"  = "amd64"
          - "os_distro"        = "Debian"
          - "os_type"          = "linux"
          - "os_version"       = "10 Buster"
          - "project_domain"   = "NORMAL"
          - "tc_env"           = "sysmon,update_sudo"
          - "volume_size"      = "20"
        } -> null
      - all_tags            = [] -> null
      - availability_zone   = "jp-pub-b" -> null
      - flavor_id           = "2718e9c1-b887-460b-bf4e-abcc2b010ec6" -> null
      - flavor_name         = "t2.c1m1" -> null
      - force_delete        = false -> null
      - id                  = "e4819f02-a442-4a0d-bf9e-d3fdfbc50533" -> null
      - image_id            = "Attempt to boot from volume - no image supplied" -> null
      - key_pair            = "<key_name>" -> null
      - name                = "k3s-node-02" -> null
      - power_state         = "active" -> null
      - region              = "JP1" -> null
      - security_groups     = [
          - "default",
        ] -> null
      - stop_before_destroy = false -> null

      - block_device {
          - boot_index            = 0 -> null
          - delete_on_termination = true -> null
          - destination_type      = "volume" -> null
          - source_type           = "image" -> null
          - uuid                  = "dee7d040-76a2-4ec0-b79c-ad6978ec08a4" -> null
          - volume_size           = 20 -> null
          - volume_type           = "General HDD" -> null
        }

      - network {
          - access_network = false -> null
          - fixed_ip_v4    = "192.168.0.100" -> null
          - mac            = "fa:16:3e:c2:91:34" -> null
          - name           = "Default Network" -> null
          - port           = "3fa67069-7588-483e-afda-c8fa6fe56dc1" -> null
          - uuid           = "3ed0220a-6c92-41a3-ad40-1b7c57127622" -> null
        }
    }

  # openstack_networking_port_v2.nic[0] will be destroyed
  - resource "openstack_networking_port_v2" "nic" {
      - admin_state_up         = true -> null
      - all_fixed_ips          = [
          - "192.168.0.99",
        ] -> null
      - all_security_group_ids = [
          - "32436712-1f5e-4b38-93af-480f08513ba9",
        ] -> null
      - all_tags               = [] -> null
      - device_id              = "bbcdf286-787e-44e7-8ca9-052b28d390d6" -> null
      - device_owner           = "compute:jp-pub-b" -> null
      - dns_assignment         = [] -> null
      - id                     = "646d9b62-b87c-44ce-9940-72ec0778e9f0" -> null
      - mac_address            = "fa:16:3e:b4:d8:4e" -> null
      - network_id             = "3ed0220a-6c92-41a3-ad40-1b7c57127622" -> null
      - port_security_enabled  = true -> null
      - region                 = "JP1" -> null
      - security_group_ids     = [
          - "32436712-1f5e-4b38-93af-480f08513ba9",
        ] -> null
      - tags                   = [] -> null
      - tenant_id              = "b07d6038ecf147138d9b0cd6a2486c48" -> null

      - binding {
          - profile     = "null" -> null
          - vif_details = {} -> null
          - vnic_type   = "normal" -> null
        }

      - fixed_ip {
          - subnet_id = "152443f7-d96c-4cf9-9ddf-2cf7fe4b8b17" -> null
        }
    }

  # openstack_networking_port_v2.nic[1] will be destroyed
  - resource "openstack_networking_port_v2" "nic" {
      - admin_state_up         = true -> null
      - all_fixed_ips          = [
          - "192.168.0.100",
        ] -> null
      - all_security_group_ids = [
          - "32436712-1f5e-4b38-93af-480f08513ba9",
        ] -> null
      - all_tags               = [] -> null
      - device_id              = "282637fe-c90a-4a3d-a7fe-e21e9f5c33c1" -> null
      - device_owner           = "compute:jp-pub-b" -> null
      - dns_assignment         = [] -> null
      - id                     = "3fa67069-7588-483e-afda-c8fa6fe56dc1" -> null
      - mac_address            = "fa:16:3e:c2:91:34" -> null
      - network_id             = "3ed0220a-6c92-41a3-ad40-1b7c57127622" -> null
      - port_security_enabled  = true -> null
      - region                 = "JP1" -> null
      - security_group_ids     = [
          - "32436712-1f5e-4b38-93af-480f08513ba9",
        ] -> null
      - tags                   = [] -> null
      - tenant_id              = "b07d6038ecf147138d9b0cd6a2486c48" -> null

      - binding {
          - profile     = "null" -> null
          - vif_details = {} -> null
          - vnic_type   = "normal" -> null
        }

      - fixed_ip {
          - subnet_id = "152443f7-d96c-4cf9-9ddf-2cf7fe4b8b17" -> null
        }
    }

Plan: 0 to add, 0 to change, 4 to destroy.

Do you really want to destroy all resources in workspace "k3s-cluster"?
  Terraform will destroy all your managed infrastructure, as shown above.
  There is no undo. Only 'yes' will be accepted to confirm.

  Enter a value:
```

### tfvars 내용 편집

`k3-cluster.tfvars` 파일의 `machines` 변수의 값을 빈 list 로 변경합니다.
<br>
```
machines = []
```

```
$ terraform plan -var-file k3s-cluster.tfvars
openstack_networking_port_v2.nic[1]: Refreshing state... [id=3fa67069-7588-483e-afda-c8fa6fe56dc1]
openstack_networking_port_v2.nic[0]: Refreshing state... [id=646d9b62-b87c-44ce-9940-72ec0778e9f0]
openstack_compute_instance_v2.vm[0]: Refreshing state... [id=bbcdf286-787e-44e7-8ca9-052b28d390d6]
openstack_compute_instance_v2.vm[1]: Refreshing state... [id=e4819f02-a442-4a0d-bf9e-d3fdfbc50533]

...(생략)
Plan: 0 to add, 0 to change, 4 to destroy.

...(생략)
```

### terraform state rm

```
$ terraform state list
data.openstack_images_image_v2.img_debian10
data.openstack_networking_network_v2.vpc
data.openstack_networking_secgroup_v2.sg
data.openstack_networking_subnet_v2.subnet
openstack_compute_instance_v2.vm[0]
openstack_compute_instance_v2.vm[1]
openstack_networking_port_v2.nic[0]
openstack_networking_port_v2.nic[1]

$ terraform state rm -dry-run openstack_compute_instance_v2.vm

Would remove openstack_compute_instance_v2.vm[0]
Would remove openstack_compute_instance_v2.vm[1]
```

`-dry-run` 옵션으로 실제 삭제는 하지 않고 확인만 해볼수도 있습니다.

## 그래프

핵심 기능은 아니지만 알고 있으면 유용할 기능중 하나로 그래프가 있습니다.
plan, plan-destroy, apply, validate, input 에 대해서 DOT 형식으로 그래프를 그려줍니다.
<br>
```
$ terraform graph

digraph {
	compound = "true"
	newrank = "true"
	subgraph "root" {
		"[root] data.openstack_images_image_v2.img_debian10 (expand)" [label = "data.openstack_images_image_v2.img_debian10", shape = "box"]
		"[root] data.openstack_networking_network_v2.vpc (expand)" [label = "data.openstack_networking_network_v2.vpc", shape = "box"]
		"[root] data.openstack_networking_secgroup_v2.sg (expand)" [label = "data.openstack_networking_secgroup_v2.sg", shape = "box"]
		"[root] data.openstack_networking_subnet_v2.subnet (expand)" [label = "data.openstack_networking_subnet_v2.subnet", shape = "box"]
		"[root] openstack_compute_instance_v2.vm (expand)" [label = "openstack_compute_instance_v2.vm", shape = "box"]
		"[root] openstack_networking_port_v2.nic (expand)" [label = "openstack_networking_port_v2.nic", shape = "box"]
		"[root] provider[\"registry.terraform.io/terraform-provider-openstack/openstack\"]" [label = "provider[\"registry.terraform.io/terraform-provider-openstack/openstack\"]", shape = "diamond"]
		"[root] var.machines" [label = "var.machines", shape = "note"]
		"[root] var.provider_openstack" [label = "var.provider_openstack", shape = "note"]
		"[root] data.openstack_images_image_v2.img_debian10 (expand)" -> "[root] provider[\"registry.terraform.io/terraform-provider-openstack/openstack\"]"
		"[root] data.openstack_networking_network_v2.vpc (expand)" -> "[root] provider[\"registry.terraform.io/terraform-provider-openstack/openstack\"]"
		"[root] data.openstack_networking_secgroup_v2.sg (expand)" -> "[root] provider[\"registry.terraform.io/terraform-provider-openstack/openstack\"]"
		"[root] data.openstack_networking_subnet_v2.subnet (expand)" -> "[root] data.openstack_networking_network_v2.vpc (expand)"
		"[root] meta.count-boundary (EachMode fixup)" -> "[root] openstack_compute_instance_v2.vm (expand)"
		"[root] openstack_compute_instance_v2.vm (expand)" -> "[root] data.openstack_images_image_v2.img_debian10 (expand)"
		"[root] openstack_compute_instance_v2.vm (expand)" -> "[root] openstack_networking_port_v2.nic (expand)"
		"[root] openstack_networking_port_v2.nic (expand)" -> "[root] data.openstack_networking_secgroup_v2.sg (expand)"
		"[root] openstack_networking_port_v2.nic (expand)" -> "[root] data.openstack_networking_subnet_v2.subnet (expand)"
		"[root] openstack_networking_port_v2.nic (expand)" -> "[root] var.machines"
		"[root] provider[\"registry.terraform.io/terraform-provider-openstack/openstack\"] (close)" -> "[root] openstack_compute_instance_v2.vm (expand)"
		"[root] provider[\"registry.terraform.io/terraform-provider-openstack/openstack\"]" -> "[root] var.provider_openstack"
		"[root] root" -> "[root] meta.count-boundary (EachMode fixup)"
		"[root] root" -> "[root] provider[\"registry.terraform.io/terraform-provider-openstack/openstack\"] (close)"
	}
}
```

결과물은 다음과 같습니다.
![image](https://user-images.githubusercontent.com/33619494/188439636-f29d3509-fb9e-4784-ae89-e9fec96a6328.png)